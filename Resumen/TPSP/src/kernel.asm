; ** por compatibilidad se omiten tildes **
; ==============================================================================
; TALLER System Programming - Arquitectura y Organizacion de Computadoras - FCEN
; ==============================================================================

%include "print.mac"

global start


; COMPLETAR - Agreguen declaraciones extern según vayan necesitando
; // Extern Parte 1 //
extern GDT_DESC
extern screen_draw_layout
extern screen_draw_box

; // Extern Parte 2 //
extern idt_init
extern IDT_DESC
extern pic_reset
extern pic_enable

; // Extern Parte 3 //
extern mmu_init_kernel_dir
extern tasks_syscall_draw
extern mmu_map_page
extern mmu_unmap_page
extern copy_page
extern mmu_init_task_dir

; // Extern Parte 4 //
extern tss_init
extern  tasks_screen_draw 
extern tss_gdt_entry_for_task
extern sched_init
extern tasks_init
extern create_task

; COMPLETAR - Definan correctamente estas constantes cuando las necesiten
%define CS_RING_0_SEL 0x8   
%define DS_RING_0_SEL 0x18  
%define KERNEL_PAGE_DIR 0x25000
%define INICIAL 0x58
%define IDLE   0x60


BITS 16
;; Saltear seccion de datos
jmp start

;;
;; Seccion de datos.
;; -------------------------------------------------------------------------- ;;
start_rm_msg db     'Iniciando kernel en Modo Real'
start_rm_len equ    $ - start_rm_msg

start_pm_msg db     'Iniciando kernel en Modo Protegido'
start_pm_len equ    $ - start_pm_msg

;;
;; Seccion de código.
;; -------------------------------------------------------------------------- ;;

;; Punto de entrada del kernel.
BITS 16
start:
    ; ==============================
    ; ||  Salto a modo protegido  ||
    ; ==============================

    ; COMPLETAR - Deshabilitar interrupciones (Parte 1: Pasaje a modo protegido)
    cli

    ; Cambiar modo de video a 80 X 50
    mov ax, 0003h
    int 10h ; set mode 03h
    xor bx, bx
    mov ax, 1112h
    int 10h ; load 8x8 font

    ; COMPLETAR - Imprimir mensaje de bienvenida - MODO REAL (Parte 1: Pasaje a modo protegido)
    ; (revisar las funciones definidas en print.mac y los mensajes se encuentran en la
    ; sección de datos)

    print_text_rm start_rm_msg, start_rm_len, 0x4, 0x0, 0x0

    ; COMPLETAR - Habilitar A20 (Parte 1: Pasaje a modo protegido)
    ; (revisar las funciones definidas en a20.asm)
    call A20_enable

    ; COMPLETAR - los defines para la GDT en defines.h y las entradas de la GDT en gdt.c
    ; COMPLETAR - Cargar la GDT (Parte 1: Pasaje a modo protegido)
    lgdt [GDT_DESC]

    ; COMPLETAR - Setear el bit PE del registro CR0 (Parte 1: Pasaje a modo protegido)
    mov eax, cr0
    or eax, 1
    mov cr0, eax

    ; COMPLETAR - Saltar a modo protegido (far jump) (Parte 1: Pasaje a modo protegido)
    ; (recuerden que un far jmp se especifica como jmp CS_selector:address)
    ; Pueden usar la constante CS_RING_0_SEL definida en este archivo
    jmp CS_RING_0_SEL:modo_protegido

BITS 32
modo_protegido:
    ; COMPLETAR (Parte 1: Pasaje a modo protegido) - A partir de aca, todo el codigo se va a ejectutar en modo protegido
    ; Establecer selectores de segmentos DS, ES, GS, FS y SS en el segmento de datos de nivel 0
    ; Pueden usar la constante DS_RING_0_SEL definida en este archivo
    mov eax, DS_RING_0_SEL
    mov DS, eax
    mov ES, eax
    mov GS, eax
    mov FS, eax
    mov SS, eax

    ; COMPLETAR - Establecer el tope y la base de la pila (Parte 1: Pasaje a modo protegido)
    mov esp, 0x25000
    mov ebp, esp

    ; COMPLETAR - Imprimir mensaje de bienvenida - MODO PROTEGIDO (Parte 1: Pasaje a modo protegido)
    print_text_pm start_pm_msg, start_pm_len, 0x4, 0x0, 0x0

    ; COMPLETAR - Inicializar pantalla (Parte 1: Pasaje a modo protegido)
    call screen_draw_layout
    
    ; ===================================
    ; ||     (Parte 3: Paginación)     ||
    ; ===================================

    ; COMPLETAR - los defines para la MMU en defines.h
    ; COMPLETAR - las funciones en mmu.c
    ; COMPLETAR - reemplazar la implementacion de la interrupcion 88 (ver comentarios en isr.asm)
    ; COMPLETAR - La rutina de atención del page fault en isr.asm
    ; COMPLETAR - Inicializar el directorio de paginas
    call mmu_init_kernel_dir
    ; COMPLETAR - Cargar directorio de paginas 
    mov eax, KERNEL_PAGE_DIR   ; dirección física del directorio de páginas
    mov cr3, eax

    ; COMPLETAR - Habilitar paginacion 
    mov eax, cr0
    or eax, 0x80000000  
    mov cr0, eax
    ; ========================
    ; ||  (Parte 4: Tareas) ||
    ; ========================

    ; COMPLETAR - reemplazar la implementacion de la interrupcion 88 (ver comentarios en isr.asm)
    ; COMPLETAR - las funciones en tss.c
    ; COMPLETAR - Inicializar tss
    call tss_init
    ; COMPLETAR - Inicializar el scheduler
    call sched_init
    ; COMPLETAR - Inicializar las tareas
    call tasks_init

    ; ===================================
    ; ||   (Parte 2: Interrupciones)   ||
    ; ===================================

    ; COMPLETAR - las funciones en idt.c
    ; COMPLETAR - Inicializar y cargar la IDT
    call idt_init
    lidt [IDT_DESC]

    ; COMPLETAR - Reiniciar y habilitar el controlador de interrupciones (ver pic.c)
    call pic_reset
    call pic_enable
    ; COMPLETAR - Rutinas de atención de reloj, teclado, e interrupciones 88 y 89 (en isr.asm)
    ; Opcional Interrupciones: Configurar PIT para ir más rapido.
    ; El divisor 4096 no es exactamente ir al doble pero es una velocidad aceptable para poder jugar en las tareas.
    mov ax, 4096           ; nuevo divisor
    out 0x40, al           ; parte baja
    rol ax, 8
    out 0x40, al           ; parte alta

    ; COMPLETAR (Parte 4: Tareas)- Cargar tarea inicial
    call tasks_screen_draw  
    mov ax, INICIAL   
    ltr ax 

    ; COMPLETAR - Habilitar interrupciones (!! en etapas posteriores, evaluar si se debe comentar este código !!)
    sti
    ; NOTA: Pueden chequear que las interrupciones funcionen forzando a que se
    ;       dispare alguna excepción (lo más sencillo es usar la instrucción
    ;       `int3`)
    ;int3

    ; COMPLETAR - Probar Sys_call (para etapas posteriores, comentar este código)
    ;int 88

    ; COMPLETAR - Probar generar una excepción (para etapas posteriores, comentar este código)
    ;mov eax, 1
    ;xor ebx, ebx
    ;div ebx
    
    ; ========================
    ; ||  (Parte 4: Tareas)  ||
    ; ========================
    cli ; desactivamos temporalmente las interrupciones para probar la tarea de prueba
    ; COMPLETAR - Inicializar el directorio de paginas de la tarea de prueba
    mov edi, 0x18000
    push edi
    call mmu_init_task_dir
    add esp, 4
    sti
    ; COMPLETAR - Cargar directorio de paginas de la tarea
    mov cr3, eax
    mov BYTE [0x07000000], 0xA  ;-> provoca page fault , se resuelve
    mov BYTE [0x07000000], 0xB  ;-> ya no provoca page fault

    ; COMPLETAR - Restaurar directorio de paginas del kernel
    mov eax, KERNEL_PAGE_DIR
    mov cr3, eax
    ; COMPLETAR - Saltar a la primera tarea: Idle
    jmp IDLE:0

    ; Ciclar infinitamente 
    mov eax, 0xFFFF
    mov ebx, 0xFFFF
    mov ecx, 0xFFFF
    mov edx, 0xFFFF
    jmp $

;; -------------------------------------------------------------------------- ;;

%include "a20.asm"
